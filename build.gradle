buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
    }
}
        
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: "com.matthewprenger.cursegradle"

def key = System.getenv("CURSETOKEN")
if(key == null)
	key = 'ddd'
curseforge {
  apiKey = key
  project {
    id = '503143'
    changelog = file('changelog.md')
    changelogType = 'markdown'
    releaseType = 'beta'
  }
}

version = System.getenv("APPVEYOR_BUILD_VERSION")
group = 'eu.gir'
archivesBaseName = 'GIRC-Redstone'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }

        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2854'
}

jar {
    manifest {
        attributes([
            "Specification-Title": "GIRC-Redstone",
            "Specification-Vendor": "GIRC",
            "Specification-Version": System.getenv("APPVEYOR_BUILD_VERSION"),
            "Implementation-Title": project.name,
            "Implementation-Version": System.getenv("APPVEYOR_BUILD_VERSION"),
            "Implementation-Vendor" :"GIRC",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"               
        expand "version":System.getenv("APPVEYOR_BUILD_VERSION")
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

jar.finalizedBy('reobfJar') 
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
