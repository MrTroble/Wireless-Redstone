buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '6.0.+', changing: true
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
    }
}
        
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: "com.matthewprenger.cursegradle"

sourceSets{
    main {
        java {
            srcDirs("linkableapi/src/")
        }
    }
}

def key = System.getenv("CURSETOKEN")
if(key == null)
	key = 'dev'
curseforge {
  apiKey = key
  project {
    id = '503143'
    changelog = file('changelog.md')
    changelogType = 'markdown'
    releaseType = 'release'
    addGameVersion 'Java 17'
  }
}


def ver = System.getenv("APPVEYOR_BUILD_VERSION")
if(ver == null)
	ver = "dev"
version = ver
group = 'com.troblecodings'
archivesBaseName = 'TC-Redstone-1.20.4'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: '1.20.4'

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            
            mods {
                gircredstone {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            
            mods {
                gircredstone {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {

}

dependencies {
    minecraft 'net.minecraftforge:forge:1.20.4-49.0.38'
}

jar {
    manifest {
        attributes([
            "Specification-Title": "TC-Redstone",
            "Specification-Vendor": "troblecodings",
            "Specification-Version": ver,
            "Implementation-Title": project.name,
            "Implementation-Version": ver,
            "Implementation-Vendor" :"troblecodings",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar') 
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
